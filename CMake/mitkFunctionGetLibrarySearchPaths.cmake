function(mitkFunctionGetLibrarySearchPaths search_path intermediate_dir)

  set(_search_dirs )
  get_property(_additional_paths GLOBAL PROPERTY MITK_ADDITIONAL_LIBRARY_SEARCH_PATHS)
  if(_additional_paths)
    foreach(_path ${_additional_paths})
      if(EXISTS "${_path}/${intermediate_dir}")
        list(APPEND _search_dirs "${_path}/${intermediate_dir}")
      else()
        list(APPEND _search_dirs ${_path})
      endif()
    endforeach()
  endif()

  if(MITK_RUNTIME_PATH)
    string(REPLACE "%VS_BUILD_TYPE%" "${intermediate_dir}" _runtime_path "${MITK_RUNTIME_PATH}")
    list(APPEND _search_dirs ${_runtime_path})
    list(REMOVE_DUPLICATES _search_dirs)
    set(${search_path} ${_search_dirs} PARENT_SCOPE)
    return()
  endif()

  foreach(_dir ${MITK_VTK_LIBRARY_DIRS} ${MITK_ITK_LIBRARY_DIRS} ${QT_LIBRARY_DIR}
               ${QT_LIBRARY_DIR}/../bin ${MITK_BINARY_DIR}/bin
               ${CMAKE_RUNTIME_OUTPUT_DIRECTORY} ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/plugins)
    if(EXISTS "${_dir}/${intermediate_dir}")
      list(APPEND _search_dirs "${_dir}/${intermediate_dir}")
    else()
      list(APPEND _search_dirs ${_dir})
    endif()
  endforeach()

  if(WIN32)
    list(APPEND _search_dirs "${ITK_DIR}/bin")
  else()
    list(APPEND _search_dirs "${ITK_DIR}/lib")
  endif()

  if(MITK_USE_Boost AND MITK_USE_Boost_LIBRARIES AND NOT MITK_USE_SYSTEM_Boost)
    list(APPEND _search_dirs ${Boost_LIBRARY_DIRS})
  endif()
  if(GDCM_DIR)
    list(APPEND _search_dirs ${GDCM_DIR}/bin/${intermediate_dir})
  endif()
  if(OpenCV_DIR)
    list(APPEND _search_dirs ${OpenCV_DIR}/bin/${intermediate_dir})
  endif()
  if(SOFA_DIR)
    list(APPEND _search_dirs ${SOFA_DIR}/bin/${intermediate_dir})
  endif()
  if(MITK_USE_TOF_PMDO3 OR MITK_USE_TOF_PMDCAMCUBE OR MITK_USE_TOF_PMDCAMBOARD)
    list(APPEND _search_dirs ${MITK_PMD_SDK_DIR}/plugins)
  endif()

  if(MITK_USE_BLUEBERRY)
    list(APPEND _search_dirs ${CTK_RUNTIME_LIBRARY_DIRS}/${intermediate_dir})
    if(DEFINED CTK_PLUGIN_RUNTIME_OUTPUT_DIRECTORY)
      if(IS_ABSOLUTE "${CTK_PLUGIN_RUNTIME_OUTPUT_DIRECTORY}")
        list(APPEND _search_dirs "${CTK_PLUGIN_RUNTIME_OUTPUT_DIRECTORY}/${intermediate_dir}")
      else()
        list(APPEND _search_dirs "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${CTK_PLUGIN_RUNTIME_OUTPUT_DIRECTORY}/${intermediate_dir}")
      endif()
    endif()
  endif()

  if(MITK_LIBRARY_DIRS)
    foreach(lib_dir ${MITK_LIBRARY_DIRS})
      if(EXISTS "${lib_dir}/${intermediate_dir}")
        list(APPEND _search_dirs "${lib_dir}/${intermediate_dir}")
      else()
        list(APPEND _search_dirs "${lib_dir}")
      endif()
    endforeach()
  endif()

  list(REMOVE_DUPLICATES _search_dirs)

  set(${search_path} ${_search_dirs} PARENT_SCOPE)

endfunction()
